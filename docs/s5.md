#### 5.33

The basic Diffie-Hellman algorithm for key exchange between Alice and
Bob goes as follows. Alice and Bob agree on a finite cyclic group of
some particular order to use. They each randomly and independently pick
some number of times to perform the group operation (which must be
greater than zero, and less than the order of the group) and perform the
group operation on the generator that number of times, publishing their
results. For generator g, Alice's number x, and Bob's number y: Alice
publishes g ^ x, and Bob publishes g ^ y.

Each then performs his or her own secret number of additional group
operations on the other's public result, establishing the key g ^ xy.
Since the discrete logarithm problem is hard, an eavesdropper can't (for
an appropriate group) determine how many times either has performed the
individual group operations, even given g; he can only naïvely calculate
g ^ (x + y).

For the initial illustration here we're using a decidedly insecure
group, i.e. the multiplicative group of 16-bit words modulo 37:

    > gen <- MWC.create
    > let p = 37
    > let g = 5
    > a <- fmap (`mod` p) (MWC.uniformR (1, p - 1) gen) :: IO Word16
    > b <- fmap (`mod` p) (MWC.uniformR (1, p - 1) gen) :: IO Word16
    > let bigA = g ^ a `mod` p
    > let bigB = g ^ b `mod` p
    > let s = bigB ^ a `mod` p
    > let t = bigA ^ b `mod` p
    > s == t
    True
    > let k = S.sha1 . BP.runPut $ BP.putWord16be s
    > k
    ace6f761db204030c1a65c0930bd01fd55ecc429

For the big guns, we can pull out GHC's Natural type, for which
mwc-random (our preferred random library) can generate something in a
range. First a modular exponentiation routine:

    -- modified from https://gist.github.com/trevordixon/6788535
    modexp :: Natural -> Natural -> Natural -> Natural
    modexp b e m
      | e == 0    = 1
      | otherwise =
          let t = if B.testBit e 0 then b `mod` m else 1
          in  t * modexp ((b * b) `mod` m) (B.shiftR e 1) m `mod` m

and given that (and appropriate p, g), the key exchange:

    > gen <- MWC.create
    > a <- fmap (`mod` p) (MWC.uniformRM (1, p - 1) gen)
    > b <- fmap (`mod` p) (MWC.uniformRM (1, p - 1) gen)
    > let bigA = modexp g a p
    > let bigB = modexp g b p
    > let s = modexp bigB a p
    > let t = modexp bigA b p
    > s == t
    True

That's all well and good, but let's have a bit of fun.

Cryptopals.DH implements the Diffie-Hellman protocol over TCP. Two
functions, 'bob' and 'alice', will initiate a TCP server and client,
respectively. Each end will then cooperate to perform Diffie-Hellman,
for the lulz then exchanging an AES128-encrypted message with the shared
key à la the initial illustration in the next challenge.

Opening two instances of GHCi, we can run 'bob' in one and 'alice' in
the other and watch the logs for fun. Here I'll interleave the relevant
parts of the logs for illustration:

    (cryptopals) bob: listening..
    (cryptopals) alice: session established
    (cryptopals) alice: sending group parameters and public key
    (cryptopals) bob: received group parameters and public key
    (cryptopals) bob: sending public key
    (cryptopals) alice: received public key
    (cryptopals) alice: sending ciphertext
    (cryptopals) bob: received ciphertext
    (cryptopals) bob: decrypted ciphertext: "attack at 10pm"
    (cryptopals) bob: replying with ciphertext
    (cryptopals) alice: received ciphertext
    (cryptopals) alice: decrypted ciphertext: "confirmed, attacking at 10pm"
    (cryptopals) bob: ending session

